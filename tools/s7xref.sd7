
(********************************************************************)
(*                                                                  *)
(*  s7xref.sd7     Seed7 Cross Reference Builder for Seed7 source   *)
(*  Copyright (C) 2025  Thomas Mertes                               *)
(*   Modified by Pierre Rouleau: Created the file from code posted  *)
(*                               by Thomas Mertes.                  *)
(*                                                                  *)
(*  This program is free software; you can redistribute it and/or   *)
(*  modify it under the terms of the GNU General Public License as  *)
(*  published by the Free Software Foundation; either version 2 of  *)
(*  the License, or (at your option) any later version.             *)
(*                                                                  *)
(*  This program is distributed in the hope that it will be useful, *)
(*  but WITHOUT ANY WARRANTY; without even the implied warranty of  *)
(*  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the   *)
(*  GNU General Public License for more details.                    *)
(*                                                                  *)
(*  You should have received a copy of the GNU General Public       *)
(*  License along with this program; if not, write to the           *)
(*  Free Software Foundation, Inc., 51 Franklin Street,             *)
(*  Fifth Floor, Boston, MA  02110-1301, USA.                       *)
(*                                                                  *)
(********************************************************************)

#  Note:
#  Compile this file with s7c to create the s7xref executable,
#  or use the s7 interpreter to run it from source.

# [:todo 2025-06-14, by Pierre Rouleau: Add command line help and protection checks.]

$ include "seed7_05.s7i";
  include "progs.s7i";


const proc: main is func
  local
    var string: fileName is "";
    var string: pgmCode is "";
    var program: aProgram is program.value;
    var ref_list: globalObjects is ref_list.EMPTY;
    var reference: anObject is NIL;
  begin
    if length(argv(PROGRAM)) >= 1 then

      fileName := argv(PROGRAM)[1];
      if endsWith(fileName, ".s7i") then
        # For a .s7i file parsing the file fails; instead create a small
        # program in a string that includes that .s7i file and parse that.
        pgmCode := "$ include \"seed7_05.s7i\";\n\
                   \  include " <& literal(fileName) <& ";\n";
        aProgram := parseStri(pgmCode);
        globalObjects := globalObjects(aProgram);
      else
        # For a .sd7 file, parse the file directly
        aProgram := parseFile(fileName);
      end if;

      # Extract all objects in both cases: what is defined inside this program
      # or library *and* what it uses; we want to be able to see where those
      # are defined.
      globalObjects := globalObjects(aProgram);
      for anObject range globalObjects do
        writeln(str(anObject) <& "\t" <& file(anObject) <& "\t" <& line(anObject));
      end for;
    end if;
  end func;

# ----------------------------------------------------------------------------
